/** Game loop controller */

class Game {

   field int score;
   field Player player;
   field Pillar pillarA, pillarB;
   field String scoreString;

   constructor Game new() {
      let score = 0;
      let player = Player.new();
      let pillarA = Pillar.new(31,8,20);
      let pillarB = Pillar.new(18,3,15);
      let scoreString = "Score: ";

      return this;
   }

   method void run() {
      var int key;
      var int columnTick;
      var int continue;
      let columnTick = 10;
      let continue = 1;
      while (continue = 1) {
         let key = Keyboard.keyPressed();
         if (columnTick = 10) {
            do pillarA.drawSelf();
            do pillarB.drawSelf();
            let columnTick=0;
         }
         let continue = player.drawSelf(key);
         do Output.moveCursor(21,52);
         do Output.printString(scoreString);
         do Output.printInt(score);
         do Sys.wait(40);
         let columnTick=columnTick+1;

         if (4 = pillarA.getLoc()) {
            let score = score + 1;
         }
         if (4 = pillarB.getLoc()) {
            let score = score + 1;
         }
      }

      do Output.moveCursor(12,25);
      do Output.printString("Game Over");
      return;
   }

   
}


